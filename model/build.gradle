evaluationDependsOn(':data')

dependencies {
    data project(path: ':data', configuration: 'archives')
}

task unpackData(type: Copy) {
    from configurations.data
    into "$buildDir/data"
    filesMatching '*.zip', { zipFileDetails ->
        copy {
            from zipTree(zipFileDetails.file)
            into destinationDir
        }
        zipFileDetails.exclude()
    }
}

task trainNetwork(type: PyExec) {
    dependsOn unpackData
    srcFiles = [file("$buildDir/data/normalized_input_features"),
                file("$buildDir/data/normalized_output_features")]
    destDirs = file("$buildDir/FFNN_models")
    pyFile = file("07_FFNN.py")
    args += srcFiles + [destDirs]
}

task packageModel(type: Zip) {
    from trainNetwork, {
        include 'frozen_model'
    }
    from unpackData, {
        include '*.pickle', '*.dict'
    }
}

artifacts {
    archives packageModel
}

publishing {
    publications {
        model(MavenPublication) {
            artifact packageModel, {
                artifactId "blizzard2017-f0-dnn-model"
            }
        }
    }
}
